#!/usr/bin/env perl
use feature 'say';
use FileHandle;

my $directory = $ENV{'SHORTCUTS_DIRECTORY'}
                        // sprintf "%s/%s", $ENV{'HOME'}, 'etc/shortcuts';


opendir my($dir), $directory;
while ( my $file = readdir $dir ) {
    next if $file =~ m{^\.};
    
    say $file;
    
    # remove any existing shortcuts
    system
        'defaults',
        'delete',
        ( $file eq 'global' ? '-globalDomain' : $file ),
        'NSUserKeyEquivalents';
    
    my $handle = FileHandle->new( sprintf '%s/%s', $directory, $file );
    my $config = do { local $/; <$handle> };
    
    foreach my $line ( split m{\n}, $config ) {
        # remove comments
        $line =~ s{#.*$}{};
        
        # entry and shortcut are separated by at least two spaces
        my( $menu_entry, $shortcut ) = split m{\s{2,}}, $line;
        
        # skip blank or useless lines
        next unless $menu_entry and $shortcut;
        
        # convert the shortcut if using human alternatives
        my $human = $shortcut;
        $shortcut =~ s{(cmd|command)-}{@};
        $shortcut =~ s{(alt|opt(?:ion)?)-}{~};
        $shortcut =~ s{shift-}{\$};
        $shortcut =~ s{(control|ctrl)-}{^};
        $shortcut =~ s{left}{\\U2190};
        $shortcut =~ s{up}{\\U2191};
        $shortcut =~ s{right}{\\U2192};
        $shortcut =~ s{down}{\\U2193};
        
        $shortcut = '\Uf704' if $shortcut eq 'F1';
        $shortcut = '\Uf705' if $shortcut eq 'F2';
        $shortcut = '\Uf706' if $shortcut eq 'F3';
        $shortcut = '\Uf707' if $shortcut eq 'F4';
        $shortcut = '\Uf708' if $shortcut eq 'F5';
        $shortcut = '\Uf709' if $shortcut eq 'F6';
        $shortcut = '\Uf70a' if $shortcut eq 'F7';
        $shortcut = '\Uf70b' if $shortcut eq 'F8';
        $shortcut = '\Uf70c' if $shortcut eq 'F9';
        $shortcut = '\Uf70d' if $shortcut eq 'F10';
        $shortcut = '\Uf70e' if $shortcut eq 'F11';
        $shortcut = '\Uf70f' if $shortcut eq 'F12';
        
        if ( $shortcut eq $human ) {
            say sprintf "    %-30s %s", $menu_entry, $shortcut;
        }
        else {
            say sprintf "    %-30s %s # %s", $menu_entry, $shortcut, $human;
        }
        
        # set new shortcut
        system
            'defaults',
            'write',
            ( $file eq 'global' ? '-globalDomain' : $file ),
            'NSUserKeyEquivalents',
            '-dict-add',
            $menu_entry,
            $shortcut;
    }
    
    say '';
}
